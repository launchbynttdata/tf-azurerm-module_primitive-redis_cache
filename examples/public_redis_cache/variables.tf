// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// variables required by resource names module

variable "resource_names_map" {
  description = "A map of key to resource_name that will be used by tf-launch-module_library-resource_name to generate resource names"
  type = map(object({
    name       = string
    max_length = optional(number, 60)
  }))

  default = {
    resource_group = {
      name       = "rg"
      max_length = 80
    }
    redis_cache = {
      name       = "redis"
      max_length = 80
    }
  }
}

variable "instance_env" {
  type        = number
  description = "Number that represents the instance of the environment."
  default     = 0

  validation {
    condition     = var.instance_env >= 0 && var.instance_env <= 999
    error_message = "Instance number should be between 0 to 999."
  }
}

variable "instance_resource" {
  type        = number
  description = "Number that represents the instance of the resource."
  default     = 0

  validation {
    condition     = var.instance_resource >= 0 && var.instance_resource <= 100
    error_message = "Instance number should be between 0 to 100."
  }
}

variable "logical_product_family" {
  type        = string
  description = <<EOF
    (Required) Name of the product family for which the resource is created.
    Example: org_name, department_name.
  EOF
  nullable    = false

  validation {
    condition     = can(regex("^[_\\-A-Za-z0-9]+$", var.logical_product_family))
    error_message = "The variable must contain letters, numbers, -, _, and .."
  }

  default = "launch"
}

variable "logical_product_service" {
  type        = string
  description = <<EOF
    (Required) Name of the product service for which the resource is created.
    For example, backend, frontend, middleware etc.
  EOF
  nullable    = false

  validation {
    condition     = can(regex("^[_\\-A-Za-z0-9]+$", var.logical_product_service))
    error_message = "The variable must contain letters, numbers, -, _, and .."
  }

  default = "redis"
}

variable "class_env" {
  type        = string
  description = "(Required) Environment where resource is going to be deployed. For example. dev, qa, uat"
  nullable    = false
  default     = "dev"

  validation {
    condition     = length(regexall("\\b \\b", var.class_env)) == 0
    error_message = "Spaces between the words are not allowed."
  }
}

variable "location" {
  description = "target resource group resource mask"
  type        = string
  default     = "eastus"
}

// variables to configure managed redis

variable "sku_name" {
  description = "The SKU of the Managed Redis instance. Examples: Balanced_B1, Balanced_B3, ComputeOptimized_X3."
  type        = string
  default     = "Balanced_B1"
}

variable "high_availability_enabled" {
  description = "Whether to enable high availability for the Managed Redis instance."
  type        = bool
  default     = true
}

variable "public_network_access" {
  description = "Public network access setting. Possible values are Enabled and Disabled."
  type        = string
  default     = "Disabled"
}

variable "default_database" {
  description = "Default database configuration block. Required when creating a new Managed Redis."
  type = object({
    access_keys_authentication_enabled            = optional(bool, false)
    client_protocol                               = optional(string, "Encrypted")
    clustering_policy                             = optional(string, "OSSCluster")
    eviction_policy                               = optional(string, "VolatileLRU")
    geo_replication_group_name                    = optional(string)
    persistence_append_only_file_backup_frequency = optional(string)
    persistence_redis_database_backup_frequency   = optional(string)
    modules = optional(list(object({
      name = string
      args = optional(string)
    })))
  })
}

variable "tags" {
  description = "Custom tags for the Managed Redis instance."
  type        = map(string)
  default     = {}
}
